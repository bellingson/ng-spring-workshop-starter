
import org.gradle.api.tasks.Exec

apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'


apply plugin: 'idea'

apply plugin: 'groovy'

repositories {
    mavenCentral()
}

String springVersion = '4.3.2.RELEASE'
String springSecurityVersion = '4.1.2.RELEASE'

configurations {
    provided
}

sourceSets.main.compileClasspath += configurations.provided

dependencies {

    provided group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    compile group: 'javax.servlet.jsp.jstl', name: 'javax.servlet.jsp.jstl-api', version: '1.2.1'
    compile group: 'org.glassfish.web', name: 'javax.servlet.jsp.jstl', version: '1.2.3', transitive: false

    compile 'org.codehaus.groovy:groovy-all:2.4.7'

    compile 'org.sitemesh:sitemesh:3.0.1'

    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
    compile group: 'org.springframework', name: 'spring-orm', version: springVersion

    compile group: 'org.springframework.security', name: 'spring-security-web', version: springSecurityVersion
    compile group: 'org.springframework.security', name: 'spring-security-config', version: springSecurityVersion

    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.6.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.7.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.7.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.5'

    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.0.Final'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.1.0.Final'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '5.2.4.Final'

    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.0.1'

    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'

    compile group: 'log4j', name: 'log4j', version: '1.2.16', ext: 'jar'

    testCompile 'junit:junit:4.12'
}

task gulp(type: Exec) {

    workingDir '.'

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'gulp'
    } else {
        commandLine 'gulp'
    }

    standardOutput = System.out
    errorOutput = System.out

}

processResources.dependsOn gulp


gretty {
    contextPath = '/'
    servletContainer = 'tomcat8'
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
        excludeDirs = [
                file(".gradle"),
                file("build"),
                file("node_modules"),
                file("src/main/js/product-mgr/node_modules"),
                file("src/main/js/product-mgr/dist"),
                file("src/main/js/product-mgr/tmp")
        ]

    }
}

